/*
 * Copyright (c) 2017-2019 TIBCO Software Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you
 * may not use this file except in compliance with the License. You
 * may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License. See accompanying
 * LICENSE file.
 */

apply plugin: 'scala'
apply plugin: 'de.undercouch.download'

compileScala.options.encoding = 'UTF-8'
// fix scala+java mix to all use compileScala which uses correct dependency order
sourceSets.main.scala.srcDir 'src/main/java'
sourceSets.test.scala.srcDirs = [ 'src/test/java', 'src/test/scala',
                                  'src/dunit/java', 'src/dunit/scala' ]
sourceSets.main.java.srcDirs = []
sourceSets.test.java.srcDirs = []

def osName = org.gradle.internal.os.OperatingSystem.current()

configurations {
  connector
}

dependencies {
  compileOnly 'org.scala-lang:scala-library:' + scalaVersion
  compileOnly 'org.scala-lang:scala-reflect:' + scalaVersion

  compile coreLibraries.common
  compile("org.apache.thrift:libthrift:${thriftVersion}") {
    exclude(group: 'org.slf4j', module: 'slf4j-api')
  }
  compile("org.parboiled:parboiled_${scalaBinaryVersion}:${parboiledVersion}") {
    exclude(group: 'org.scala-lang', module: 'scala-library')
    exclude(group: 'org.scala-lang', module: 'scala-reflect')
    exclude(group: 'org.scala-lang', module: 'scala-compiler')
  }
  compileOnly "com.rabbitmq:amqp-client:${rabbitMqVersion}"

  // always use stock spark so that snappy extensions don't get accidently
  // included here in snappy-core code.
  if (System.properties.containsKey('ideaBuild') && new File(rootDir, 'spark/build.gradle').exists()) {
    compile project(':snappy-spark:snappy-spark-core_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-catalyst_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-sql_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-hive_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-streaming_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-streaming-kafka-0.10_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-sql-kafka-0.10_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-mllib_' + scalaBinaryVersion)

    compileOnly "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    testCompile "org.eclipse.jetty:jetty-servlet:${jettyVersion}"

    testCompile project(path: ':snappy-spark:snappy-spark-core_' + scalaBinaryVersion, configuration: 'testOutput')
    testCompile project(path: ':snappy-spark:snappy-spark-sql_' + scalaBinaryVersion, configuration: 'testOutput')
    testCompile project(path: ':snappy-spark:snappy-spark-sql-kafka-0.10_' + scalaBinaryVersion, configuration: 'testOutput')
    testCompile project(path: ':snappy-spark:snappy-spark-streaming_' + scalaBinaryVersion, configuration: 'testOutput')
    testCompile project(path: ':snappy-spark:snappy-spark-streaming-kafka-0.10_' + scalaBinaryVersion, configuration: 'testOutput')
  } else {
    compileOnly coreLibraries.spark
    testCompile coreLibraries.spark

    testCompile("org.apache.spark:spark-core_${scalaBinaryVersion}:${sparkConnectorVersion}:tests")
    testCompile("org.apache.spark:spark-sql_${scalaBinaryVersion}:${sparkConnectorVersion}:tests")
    testCompile("org.apache.spark:spark-sql-kafka-0-10_${scalaBinaryVersion}:${sparkConnectorVersion}:tests")
    testCompile("org.apache.spark:spark-streaming_${scalaBinaryVersion}:${sparkConnectorVersion}:tests")
    testCompile("org.apache.spark:spark-streaming-kafka-0-10_${scalaBinaryVersion}:${sparkConnectorVersion}:tests")
  }

  if (new File(rootDir, 'store/build.gradle').exists()) {
    compile project(':snappy-store:snappydata-store-client')
    compile project(':snappy-store:snappydata-store-core')
    compile project(':snappy-store:snappydata-store-tools')
    testCompile project(path: ':snappy-store:snappydata-store-tools', configuration: 'testOutput')
  } else {
    compile group: 'io.snappydata', name: 'snappydata-store-client', version: snappyStoreVersion
    compile group: 'io.snappydata', name: 'snappydata-store-core', version: snappyStoreVersion
    compile group: 'io.snappydata', name: 'snappydata-store-tools', version: snappyStoreVersion
    testCompile group: 'io.snappydata', name: 'snappydata-store-tools', version: snappyStoreVersion, classifier: 'tests'
  }
  compile project(":snappy-jdbc_${scalaBinaryVersion}")
  compile project(":snappy-encoders_${scalaBinaryVersion}")

  connector project(compatConnectorProjectName)

  testCompile project(':dunit')
  testCompile 'org.scala-lang:scala-actors:' + scalaVersion
  testCompile "org.scalatest:scalatest_${scalaBinaryVersion}:${scalatestVersion}"
  testCompile(group: 'org.apache.kafka', name: 'kafka_' + scalaBinaryVersion, version: kafka2Version) {
    exclude(group: 'net.jpountz.lz4', module: 'lz4')
    exclude(group: 'com.fasterxml.jackson.core', module: 'jackson-core')
    exclude(group: 'com.fasterxml.jackson.core', module: 'jackson-databind')
    exclude(group: 'com.fasterxml.jackson.core', module: 'jackson-annotations')
  }

  testCompile project(compatConnectorProjectName)

  testRuntime files("${projectDir}/../tests/common/src/main/resources")
  testRuntime "org.pegdown:pegdown:${pegdownVersion}"
  testCompile(project(path: ':snappy-examples_' + scalaBinaryVersion, configuration: 'testOutput')) {
    exclude(group: 'io.snappydata', module: 'snappy-cluster_' + scalaBinaryVersion)
    exclude(group: 'io.snappydata', module: 'snappy-aqp_' + scalaBinaryVersion)
    exclude(group: 'io.snappydata', module: 'gemfire-core')
  }
}

task packageScalaDocs(type: Jar, dependsOn: scaladoc) {
  classifier = 'javadoc'
  from scaladoc
}
if (rootProject.hasProperty('enablePublish')) {
  artifacts {
    archives packageScalaDocs, packageSources
  }
}

scalaTest {
  dependsOn ':cleanScalaTest'
  doFirst {
    // cleanup files since scalatest plugin does not honour workingDir yet
    cleanIntermediateFiles(project.path)
  }
  doLast {
    // cleanup files since scalatest plugin does not honour workingDir yet
    cleanIntermediateFiles(project.path)
  }
}

def downloadApacheSparkDist(String ver, String distName, String prodDir) {
  return tasks.create("downloadApache${ver}SparkDist", Download) {
    outputs.files "${prodDir}.tgz", "${prodDir}/README.md"

    src "http://archive.apache.org/dist/spark/spark-${ver}/${distName}.tgz"
    dest sparkDistDir
    onlyIfNewer true

    doFirst {
      mkdir(sparkDistDir)
    }
  }
}

def taskGetApacheSparkDist(String ver, String distName, String prodDir) {
  return tasks.create("getApacheSpark${ver}Dist") {
    dependsOn downloadApacheSparkDist(ver, distName, prodDir)

    outputs.files "${prodDir}.tgz", "${prodDir}/README.md"

    doLast {
      if (osName.isWindows()) {
        copy {
          from tarTree(resources.gzip("${sparkDistDir}/${distName}.tgz"))
          into sparkDistDir
        }
      } else {
        // gradle tarTree does not preserve symlinks (GRADLE-2844)
        exec {
          executable 'tar'
          args 'xzf', "${distName}.tgz"
          workingDir = sparkDistDir
        }
      }
    }
  }
}

task getApacheSparkDist {
  dependsOn taskGetApacheSparkDist(sparkConnectorVersion, sparkConnectorDistName, sparkConnectorProductDir)
  if (sparkConnectorVersion != sparkCurrentVersion) {
    dependsOn taskGetApacheSparkDist(sparkCurrentVersion, sparkCurrentDistName, sparkCurrentProductDir)
  }
}

test.dependsOn ':cleanJUnit'
dunitTest.dependsOn getApacheSparkDist
dunitSecurityTest.dependsOn getApacheSparkDist
// SplitClusterDUnitSecurityTest.testSnappyStreamingJob needs cluster tests
dunitSecurityTest.dependsOn ":snappy-cluster_${scalaBinaryVersion}:testClasses"
check.dependsOn test, scalaTest, dunitTest, dunitSecurityTest

archivesBaseName = "snappydata-core-spark${sparkConnectorVersion}_${scalaBinaryVersion}"

shadowJar {
  zip64 = true
  // relocate to avoid conflict with any incompatible version in classpath (e.g. 0.9.2)
  relocate 'org.apache.thrift', 'io.snappydata.org.apache.thrift'
  // relocate eclipse collections for possible conflicts with user dependencies
  relocate 'org.eclipse.collections', 'io.snappydata.org.eclipse.collections'
  // relocate guava's com.google packages since it frequently conflicts with external guava dependencies
  relocate 'com.google.common', 'io.snappydata.com.google.common'

  configurations = [ project.configurations.runtime, project.configurations.connector ]

  mergeServiceFiles()
  exclude 'log4j.properties'

  if (rootProject.hasProperty('enablePublish')) {
    createdBy = vendorName
  } else {
    createdBy = System.getProperty('user.name')
  }
  manifest {
    attributes(
      'Manifest-Version'  : '1.0',
      'Created-By'        : createdBy,
      'Title'             : archivesBaseName,
      'Version'           : version,
      'Vendor'            : vendorName
    )
  }
}

// write the POM for spark-package
task sparkPackagePom(dependsOn: shadowJar) { doLast {
  file("${rootProject.buildDir}/distributions").mkdirs()
  pom {
    project {
      groupId 'SnappyDataInc'
      artifactId 'snappydata'
      version "${version}-s_${scalaBinaryVersion}"
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
    whenConfigured { p -> p.dependencies.clear() }
  }.writeTo("${rootProject.buildDir}/distributions/${sparkPackageName}.pom")
  copy {
    from "${buildDir}/libs"
    into "${rootProject.buildDir}/distributions"
    include "${shadowJar.archiveName}"
    rename { filename -> "${sparkPackageName}.jar" }
  }
} }

task sparkPackage(type: Zip, dependsOn: sparkPackagePom) {
  archiveName = "${sparkPackageName}.zip"
  destinationDir = file("${rootProject.buildDir}/distributions")
  outputs.upToDateWhen { false }

  from ("${rootProject.buildDir}/distributions") {
    include "${sparkPackageName}.jar"
    include "${sparkPackageName}.pom"
  }
}
