/*
 * Copyright (c) 2017-2020 TIBCO Software Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you
 * may not use this file except in compliance with the License. You
 * may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License. See accompanying
 * LICENSE file.
 */

apply plugin: 'scala'

compileScala.options.encoding = 'UTF-8'

// directly include the base classes to ensure everything is compiled with current Spark version
sourceSets.main.scala.srcDir '../spark-2.3/src/main/scala'
sourceSets.main.scala.srcDir '../spark-2.3/src/main/java'
sourceSets.main.scala.srcDir 'src/main/java'
sourceSets.main.java.srcDirs = []

// keeping this as sparkConnectorVersion helps use the same for multiple Spark versions
// for using the same gradle build across 2.4.x versions
String sparkCompatVersion = (sparkConnectorVersion ==~ /2.4.*/) ? sparkConnectorVersion : '2.4.7'

dependencies {
  compileOnly 'org.scala-lang:scala-library:' + scalaVersion
  compileOnly 'org.scala-lang:scala-reflect:' + scalaVersion

  compileOnly 'org.slf4j:slf4j-api:' + slf4jVersion
  compileOnly 'org.slf4j:slf4j-log4j12:' + slf4jVersion
  compileOnly 'org.slf4j:jcl-over-slf4j:' + slf4jVersion
  compileOnly 'org.slf4j:jul-to-slf4j:' + slf4jVersion

  if (System.properties.containsKey('ideaBuild') && new File(rootDir, 'spark/build.gradle').exists()) {
    compile project(':snappy-spark:snappy-spark-core_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-catalyst_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-sql_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-hive_' + scalaBinaryVersion)
    compile project(':snappy-spark:snappy-spark-streaming_' + scalaBinaryVersion)
  } else {
    compileOnly("org.apache.spark:spark-core_${scalaBinaryVersion}:${sparkCompatVersion}")
    compileOnly("org.apache.spark:spark-catalyst_${scalaBinaryVersion}:${sparkCompatVersion}")
    compileOnly("org.apache.spark:spark-sql_${scalaBinaryVersion}:${sparkCompatVersion}")
    compileOnly("org.apache.spark:spark-hive_${scalaBinaryVersion}:${sparkCompatVersion}")
    compileOnly("org.apache.spark:spark-streaming_${scalaBinaryVersion}:${sparkCompatVersion}")
  }

  compileOnly "org.eclipse.jetty:jetty-servlet:${jettyVersion}"

  compileOnly(project(":snappy-core_${scalaBinaryVersion}")) {
    transitive = false
  }
  compileOnly(project(":snappy-jdbc_${scalaBinaryVersion}")) {
    transitive = false
  }
  compileOnly project(':snappy-store:snappydata-store-core')
}

archivesBaseName = "snappydata-core-compat-spark${sparkCompatVersion}_${scalaBinaryVersion}"
sparkPackageName = "snappydata-${version}_${sparkCompatVersion}-s_${scalaBinaryVersion}"
